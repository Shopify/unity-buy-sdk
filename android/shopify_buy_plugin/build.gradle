repositories {
    maven {
        url 'https://maven.google.com'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

checkstyle {
    toolVersion = "7.5.1" // http://checkstyle.sourceforge.net/releasenotes.html
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.mockito:mockito-android:2.7.15'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'

    compile project(':unity_classes')
    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.android.support:customtabs:26.0.0'
    compile 'com.android.support:design:26.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.shopify.mobilebuysdk:buy3-pay-support:1.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.json:json:20140107'
}

task copyBinariesToPlugins(type: Copy) {
    def destination = '../../Assets/Shopify/Plugins/Android/libs'
    from('build/outputs/aar') {
        rename "shopify_buy_plugin-release.aar", "shopify_buy_plugin.aar"
        exclude '*-debug.aar'
    }
    into destination
    configurations.compile.files.findAll { file ->
        !file.name.equals("classes.jar") &&
                !file.absolutePath.matches(".*com.android.support.*") &&
                !file.absolutePath.matches(".*com.google.android.*") &&
                !file.absolutePath.matches(".*android.arch.*")
    } each {
        println "Copying ${it} to ${destination}"
        from(it.parent) {
            include '*.jar'
            include '*.aar'
        } into destination
    }
    outputs.upToDateWhen { false }
}

task assembleUnity {
    dependsOn 'assembleRelease', 'copyBinariesToPlugins'
}

copyBinariesToPlugins.mustRunAfter('assembleRelease')

task checkstyle(type: Checkstyle) {
    configFile = new File(rootDir, "checkstyle/config.xml")
    configProperties.checkstyleSuppressionsPath = new File(rootDir, "checkstyle/suppressions.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}


